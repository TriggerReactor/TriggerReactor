import org.yaml.snakeyaml.Yaml

buildscript {
  	ext.bukkit_version = "1.12-R0.1-SNAPSHOT"
  	ext.plugin_version = "1.1.3"
  	
  	repositories {
		jcenter()
	}
	
  	dependencies {
		classpath 'org.yaml:snakeyaml:1.15'
    }
}

plugins {
	id 'java'
	id 'groovy'
	id 'eclipse'
    id 'org.spongepowered.plugin' version '0.8.1'
}

group = 'io.github.wysohn.triggerreactor'
description = 'Simple script parser with infinite possibility'

configurations {
	jaxDoclet
}

repositories {
	jcenter()
	mavenCentral()
	maven {
		url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
	}
	maven {
		url "http://nexus.hc.to/content/repositories/pub_releases/"
	}
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
	maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}


sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		resources {
			srcDirs = ['src/main/resources']
			include 'Executor/*.js'
			include 'Executor/*/*.js'
			include '*.yml'
		}
	}
	
	test{
	 	java {
            srcDirs = ['src/test/java/']
        }
	}
}

dependencies {
	compile fileTree(dir: "libs", includes: ['*.jar'])
	
	//bukkit
	compile "org.spigotmc:spigot-api:$bukkit_version"
	
	compile "net.milkbowl.vault:VaultAPI:1.6"
	//////////////////////////////////////////////////////////
	//sponge
	compile 'org.spongepowered:spongeapi:6.0.0'
	
	//////////////////////////////////////////////////////////
	
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:2.+'
}

task fileYaml {
	doLast{
		def yaml = new Yaml()
		def doc = new File("./src/main/resources/plugin.yml")
		def map = (Map) yaml.load(doc)
		map["version"] = "$plugin_version"
		def newYaml = yaml.dump(map)
		println(newYaml)
		def newDoc = new File("./src/main/resources/plugin.yml")
		newDoc.write(newYaml);
	}
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
	failOnError = false
	options { 
       links "https://docs.oracle.com/javase/8/docs/api/" 
       links "https://hub.spigotmc.org/javadocs/bukkit/"
    } 
}
